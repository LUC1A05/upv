EGOERA			EQU	R7
GERTAERA		EQU	R6	
ARRAB_ABIADURA		EQU	R5		;Bi arrabol-jokoen biraketa-abiadura kontrolatzeko kanala (h eta b)
HAIZE_ABIADURA		EQU	R4		;Lehortzeko haizagailuen biraketa-abiadura kontrolatzeko kanala

;ATAKA 0
SEM_VERDE		EQU	P0.0		;Semaforoa berdean dago
SEM_ROJO		EQU	P0.1		;Semaforoa gorrian dago
S_PLAT			EQU	P0.2		;Kotxearen gurpila plataformaren gainean dago
FICHA_TIPO		EQU	P0.3		;1 denean garbiketa bizia/intensiboa, 0 normala
S_FICHA 		EQU	P0.4		;Fitxa makinan sartu da	
P_START			EQU	P0.5		;Hasiera pultsadorea
AL_START 		EQU	P0.6		;Alarma piztu da 
BPOS_START		EQU	P0.7		;Garbiketa-zubiaren amaiera/atseden/hasierako posizioan dago

;ATAKA 1
BMOV_FRONT		EQU	P1.0 		;Garbiketa-zubia aurrerantz mugitu makinaren noranzkoan
BMOV_BACK		EQU	P1.1 		;Garbiketa-zubia atzerantz mugitu makinaren noranzkoan
FC_RH_TOP		EQU	P1.2		;Arrabol horizontala goi mugara heldu da 
RH_UPTO			EQU	P1.3		;Arrabol horizontala gorantz mugitu
RH_DOWNTO		EQU	P1.4		;Arrabol horizontala beherantz mugitu
FC_RV_BORDE		EQU	P1.5		;Arrabol bertikalak aldeetako mugara heldu dira
RV_OUTTO		EQU	P1.6		;Arrabol bertikalak kanporantz mugitu
RV_INTO			EQU	P1.7		;Arrabol bertikalak barrurantz mugitu

;ATAKA 2
EV_AGUA			EQU	P2.0		;Aktibatu makinako ur-ponpa
EV_JABON		EQU	P2.1		;Xaboidun ur ponpa aktibatu
S_CAR			EQU	P2.2		;Ibilgailluaren sentsorea garbiketa zubiaren ardatzean
FC_RV_CENTRO		EQU	P2.3		;Arrabol bertikalen karrera amaierako sentsorea erdian/barruan
LED_LNOR		EQU	P2.4		;Sartutako fitxa garbiketa "normala" motakoa denean
LED_LINT		EQU	P2.5		;Sartutako fitxa garbiketa "bizia" motakoa denean

;TIMERRERAKO KONTADOREAK
KONT_50ms 		EQU 	0x20
KONT_1s 		EQU 	0x21
KONT_100ms		EQU	0x27
TICK_1s			EQU	0x22.0
TICK_4s			EQU	0x22.1
TICK_30s		EQU	0x22.2
TICK_100ms		EQU	0x22.3
	
;A/D ATAKA
TICK_ADC0		EQU	0x22.4		;Distantzia sentsorea arrabol horizontalen biraketa-ardatzarekiko
TICK_ADC1		EQU	0x22.5		;Distantzia sentsorea arrabol bertikalen biraketa-ardatzarekiko

;BESTE ETIKETAK
LEHEN_ALDIA		EQU	0x22.6		;30s lehenengo aldiz pasatu diren esango digu. 
EGINDA			EQU	0x22.7		;
HOR_KOKATUTA		EQU	0x23.0
BERT_KOKATUTA		EQU	0x23.1
HANDIAGOAH		EQU	0x23.2
HANDIAGOAB		EQU	0x23.3
LEHENAH			EQU	0x23.4
LEHENAB			EQU	0x23.5
ORAIN			EQU	0x24
LEHEN			EQU	0x25
ERTZA			EQU	0x26

;LIB
ADCH			EQU	0xC6
ADCON			EQU	0xC5
PWMP			EQU	0xFE
PWM0			EQU	0xFC
PWM1			EQU	0xFD
IEN0			EQU	0xA8


ORG 0x00
	AJMP	HASIERA

;*********************************ETENAK*************************
;TIMER
ORG 0x0B
	PUSH	ACC			;ACC erregistroaren edukia gordetzen da 
	PUSH	PSW			;PSW erregistroaren edukia gordetzen da
	ACALL	TIMER_AZPI
	POP	PSW			;PSW erregistroaren egoera leheneratzen da
	POP	ACC			;ACC erregistroaren egoera leheneratzen da
	RETI				;reti etenentzako bakarrik (Return from Interruption)
;ADC
ORG 0x53 
	PUSH	ACC
	PUSH	PSW
	ACALL	ADC_AZPI
	POP	PSW
	POP	ACC
	RETI
;*********************************PROGRAMA NAGUSIA*************************
ORG 0x7B
HASIERA:
	ACALL	HASIERAKETAK
LOOP:
	ACALL	EGOERA_MAKINA
	AJMP	LOOP
;**********************************HASIERAKETAK***************************
HASIERAKETAK:
	MOV	EGOERA, #0x06
	MOV	GERTAERA, #0X00
	MOV	KONT_50ms, #0x00
	MOV	KONT_1s, #0x00
	CLR	LEHEN_ALDIA
	CLR	EGINDA
	CLR	HOR_KOKATUTA
	CLR 	BERT_KOKATUTA
	CLR	HANDIAGOAH
	CLR	HANDIAGOAB
	CLR	LEHENAH
	CLR	LEHENAB
	CLR	TICK_1s
	CLR	TICK_4s
	CLR	TICK_30s
	CLR	TICK_100ms
	CLR	TICK_ADC0
	CLR	TICK_ADC1

	SETB 	SEM_VERDE
	CLR	SEM_ROJO
	CLR	FICHA_TIPO
	CLR	S_FICHA
	CLR	AL_START
	CLR	BMOV_FRONT
	CLR	BMOV_BACK
	CLR	RH_UPTO
	CLR	RH_DOWNTO
	CLR	RV_OUTTO
	CLR	RV_INTO
	CLR	EV_AGUA
	CLR 	EV_JABON
	CLR	LED_LNOR
	CLR	LED_LINT

	MOV	TMOD, #0x01	;TIMER0 16 BIT MODUA

	MOV	PWMP, #0x18
	MOV	PWM0, #0xFF	;ITZALITA
	MOV	PWM1, #0xFF
	
	MOV	IEN0, #0x00
	;ITZALI HURRENGO SEINALEAK ESKUZ:
	;P0.5(P_START)
	;P0.2(S_PLAT)
	;P2.2(S_CAR)
	;P2.3(FC_RV_CENTRO)
	RET

;********************************EGOERA_MAKINA**********************
EGOERA_MAKINA:
	MOV	A, EGOERA		;egoera akumuladorean gorde
	RL	A			;bider 2 egin, AJMP instrukzio bakoitzak 2 byte okupatzen dituelako
	MOV	DPTR, #EGOERA_TAULA	;egoera taularen memoria helbidea DPTR erregistroan gorde
	JMP	@ A+DPTR		;egoera taularen helbideari dagokion egoeraren balioa (bider 2) gehitu eta horra salto egin
EGOERA_TAULA:
	AJMP EGOERA_0		; 	ITXOITEN
	AJMP EGOERA_1		;	FITXA ITXOITEN
	AJMP EGOERA_2		;	HASI ITXOITEN
	AJMP EGOERA_3		; 	XABOIA ITXOITEN
 	AJMP EGOERA_4		; 	XABOIA 
	AJMP EGOERA_5		;	IGURKETA BERTIKALA ATZEALDEA
	AJMP EGOERA_6		;	ARRABOLAK KOKATU
	AJMP EGOERA_7		;	IGURKETA AURRERANTZ
	AJMP EGOERA_8		;	IGURKETA BERTIKALA AURREALDEA
	AJMP EGOERA_9		;	XABOIA KENDU
	AJMP EGOERA_10	;	LEHORTU
	AJMP EGOERA_11	;	KADENTZIA

;***********************************EGOERA_0 (ITXOITEN)******************************
EGOERA_0:
	ACALL	GERT_SOR_0
	MOV	A,GERTAERA
	RL 	A
	MOV 	DPTR, #LISTA_GERT_0
	JMP 	@A+DPTR

LISTA_GERT_0:
	AJMP	EM0_GER0			;ez dago kotxerik?  itxaron
	AJMP	EM0_GER1			;kotxea dago? egoera 1

GERT_SOR_0:
	JB	S_PLAT, KOTXEA_DETEKTATUTA	;kotxea plataforma gainean dago?
	MOV 	GERTAERA, #0x00			;Ez dago kotxerik? gertaera0
	RET
KOTXEA_DETEKTATUTA:
	MOV	GERTAERA,#0X01			;Badago kotxea? geratera 1
	RET
EM0_GER0:
	RET
EM0_GER1:
	SETB	SEM_ROJO			;semaforoa gorria piztu
	CLR	SEM_VERDE			;semaforoa berdea itzali
	ACALL 	TIMER_PIZTU			;tenporizadorea aktibatu
	MOV	EGOERA,#0x01			;egoera 1era pasatu
	RET

;***********************************EGOERA_1 (FITXA ITXOITEN)***********************
EGOERA_1:
	ACALL	GERT_SOR_1			
	MOV	A, GERTAERA
	RL	A
	MOV	DPTR, #LISTA_GERT_1
	JMP	@A+DPTR
LISTA_GERT_1:
	AJMP	EM1_GER0			;Ez da ezer gertatzen
	AJMP	EM1_GER1			;Kotxea plataformatik kendu da
	AJMP	EM1_GER2			;Bezeroak fitxa bat sartu da
	AJMP	EM1_GER3			;Itxarote denbora pasatu da: 30s
GERT_SOR_1:
	JNB	S_PLAT ,E1_KENDUTA		;Kotxea ez badago plataforman
	JB	S_FICHA,E1_FICHA		;Fitxa sartu bada
	JNB	TICK_30s, E1_EZER_EZ		;Ez badira 30s pasatu
	MOV	GERTAERA, #0x03			;30s-ak pasatu badira
	RET	
E1_EZER_EZ:					
	MOV 	GERTAERA, #0x00			;Ez bada ezer gertatu, gertaera 0
	RET
E1_FICHA:
	MOV	GERTAERA,#0x02			;Fitxa sartu bada, gertaera 2
	RET
E1_KENDUTA:
	MOV	GERTAERA,#0x01			;Kotxea plataformatik kendu bada, gertaera 1
	RET
EM1_GER0:					;EZ DA EZER EZ GERTATU
	RET					;Ez da ezer ez egingo
EM1_GER1:					;KOTXEA PLATAFORMATIK KENDU DA:
	ACALL	TIMER_AMATATU			;Tenporizadorea desaktibatu
	CLR	AL_START			;Alarma itzali	
	MOV	EGOERA, #0x00			;0 egoerara aldatu ITXOIN
	SETB	SEM_VERDE			;Semaforoa berdera piztu
	CLR	SEM_ROJO			;Semaforo gorria itzali
	RET
EM1_GER2:					;BEZEROAK FITXA BAT SARTU DU
	ACALL	TIMER_AMATATU			;T enporizadorea desaktibatu
	CLR	AL_START			;Alarma itzali
	MOV	EGOERA,#0x02			;2 egoerara aldatu HASI ITXOITEN
	MOV	LEHEN, P0
	ACALL	FITXA_MOTA			;Garbiketa bizia edo normala den jakiteko
	ACALL	TIMER_PIZTU			;Tenporizadorea aktibatu
	RET
FITXA_MOTA:					;BIZIA EDO NORMALA?	
	JB	FICHA_TIPO, GARB_BIZIA		;Garbiketa bizia hasi
	SETB	LED_LNOR			;NORMALA:
	MOV	ARRAB_ABIADURA, #0x80		;PWM0 %50 = Arrabolen abiadura 
	MOV	HAIZE_ABIADURA, #0x80		;PWM1 %50 = Lehortzeko haizagailuen abiadura
	RET
GARB_BIZIA:					
	SETB	LED_LINT			;BIZIA:
	MOV	ARRAB_ABIADURA, #0x00		;PWM0 %100 = Arrabolen abiadura
	MOV	HAIZE_ABIADURA, #0x26		;PWM1 %85 = Lehortzeko haizagailuen abiadura
	RET
EM1_GER3:					;ITXAROTE DENBORA (30s) PASATU DIRA
	ACALL	TIMER_AMATATU			;Tenporizadorea desaktibatu
	SETB	AL_START			;Alarma piztu
	RET

;***********************************EGOERA_2 (HASI ITXOITEN)***********************
EGOERA_2:
	ACALL	GERT_SOR_2
	MOV	A,GERTAERA
	RL	A
	MOV	DPTR,#LISTA_GERT_2
	JMP	@A+DPTR
LISTA_GERT_2:
	AJMP	EM2_GER0			;Ez da ezer gertatzen
	AJMP	EM2_GER1			;30 lehen aldiz
	AJMP	EM2_GER2			;30 bigarren aldiz, aldatu 3. egoerara
GERT_SOR_2:
	MOV	ORAIN, P0
	ACALL	ERTZAK
	MOV	A, ERTZA
	JNZ	E2_START				;Bezeroak hasteko botoiari eman badio
	JNB	TICK_30s, E2_EZER_EZ		;30s ez badira pasatu
	JB	LEHEN_ALDIA, E2_START		;30s pasatu diren lehen aldia ez bada
	MOV	GERTAERA, #0x01				;30s-ak pasatu badira ? geratera 1
	RET
ERTZAK:
	MOV	A, ORAIN
	ANL	A,#0x20
	MOV	B, LEHEN
	ANL	B,#0x20
	XRL	A, B
	MOV	ERTZA, A
	MOV	B, ORAIN
	MOV	LEHEN, B
	RET
E2_EZER_EZ:
	MOV	GERTAERA, #0x00			;Ez bada ezer ez gertatu ? gertaera 0
	RET
E2_START:
	CLR	LEHEN_ALDIA
	MOV	GERTAERA,#0x02
	RET
EM2_GER0:
	RET
EM2_GER1:
	ACALL	TIMER_AMATATU
	SETB	AL_START
	SETB	LEHEN_ALDIA
	ACALL	TIMER_PIZTU
	RET
EM2_GER2:
	ACALL	TIMER_AMATATU
	CLR	AL_START
	SETB	BMOV_FRONT
	MOV	EGOERA, #0x03
	RET
;***********************************EGOERA_3 (XABOIA ITXOITEN)***********************
EGOERA_3:
	ACALL	GERT_SOR_3
	MOV	A, GERTAERA
	RL	A
	MOV	DPTR, #LISTA_GERT_3
	JMP	@A+DPTR
LISTA_GERT_3:
	AJMP	EM3_GER0
	AJMP	EM3_GER1
GERT_SOR_3:
	JB	S_CAR,E3_CAR
	MOV	GERTAERA, #0x00
	RET
E3_CAR:
	MOV	GERTAERA,#0x01
	RET
EM3_GER0:
	RET
EM3_GER1:
	MOV	EGOERA,#0x04
	SETB	EV_AGUA
	SETB	EV_JABON
	RET

;***********************************EGOERA_4 (XABOIA)***********************
EGOERA_4:
	ACALL	GERT_SOR_4
	MOV	A, GERTAERA
	RL	A
	MOV	DPTR, #LISTA_GERT_4
	JMP	@A+DPTR
LISTA_GERT_4:
	AJMP	EM4_GER0
	AJMP	EM4_GER1
GERT_SOR_4:
	JNB	S_CAR,E4_CAR
	MOV	GERTAERA, #0X00
	RET
E4_CAR:
	MOV	GERTAERA,#0X01
	RET
EM4_GER0:
	RET
EM4_GER1:
	ACALL	TIMER_PIZTU
	CLR	EV_AGUA
	CLR	EV_JABON
	MOV	EGOERA,#0X05
	RET

;****************************EGOERA_5 (IGURKETA BERTIKALA ATZEALDEA)***********************
EGOERA_5:
	ACALL	GERT_SOR_5
	MOV	A, GERTAERA
	RL	A
	MOV	DPTR, #LISTA_GERT_5
	JMP	@A+DPTR
LISTA_GERT_5:
	AJMP	EM5_GER0	;EZ DA EZER GERTATZEN
	AJMP	EM5_GER1	;TICK4 PIZTU DA
	AJMP	EM5_GER2	;ERDIAN DAGO
	AJMP	EM5_GER3	;BORDEAN DAGO
GERT_SOR_5:
	JB	TICK_4s, E5_TIMER
	JB	EGINDA, E5_KANPOAN
	JB	FC_RV_CENTRO, E5_ERDIAN
E5_EZER_EZ:
	MOV	GERTAERA,#0X00
	RET
E5_ERDIAN:
	MOV	GERTAERA,#0X02
	RET
E5_KANPOAN:
	JNB	FC_RV_BORDE,E5_EZER_EZ
	MOV	GERTAERA,#0x03
	RET
E5_TIMER:
	MOV	GERTAERA,#0x01
	RET
EM5_GER0:
	RET
EM5_GER1:
	ACALL	TIMER_AMATATU
	CLR	BMOV_FRONT
	MOV	PWM0, ARRAB_ABIADURA
	SETB	RV_INTO
	;FC_RV_BORDE amatatu
	RET
EM5_GER2:
	SETB	EGINDA
	CLR	RV_INTO
	SETB	RV_OUTTO
	RET
EM5_GER3:
	CLR	EGINDA
	CLR	RV_OUTTO
	SETB	BMOV_BACK
	MOV	EGOERA,#0x06
	RET

;****************************EGOERA_6 (ARRABOLAK KOKATU)***********************
EGOERA_6:
	ACALL	GERT_SOR_6
	MOV	A, GERTAERA
	RL	A
	MOV	DPTR, #LISTA_GERT_6
	JMP	@A+DPTR
LISTA_GERT_6:
	AJMP	EM6_GER0	;ezer ez
	AJMP	EM6_GER1	;gelditu (kotxea aurkituta)
	AJMP	EM6_GER2	;tick 100ms horizontala txarto
	AJMP	EM6_GER3	;tick 100ms horizontala ondo bertikala txarto
	AJMP	EM6_GER4	;horizontala ondo bertikala txarto
	AJMP	EM6_GER5	;horizontala txarto bertikala ondo
	AJMP	EM6_GER6	;biak ondo (horizontala)
	AJMP	EM6_GER7	;biak ondo (bertikala)
GERT_SOR_6:
	JB	TICK_100ms, E6_HASI
	JB	TICK_ADC0, E6_HORIZONTALA
	JB	TICK_ADC1, E6_BERTIKALA
	JB	S_CAR,E6_GELDITU
E6_EZER_EZ:
	MOV	GERTAERA,#0x00
	RET
E6_GELDITU:
	JB	LEHEN_ALDIA, E6_EZER_EZ
	MOV	GERTAERA,#0x01
	RET
E6_HASI:
	CLR	TICK_100ms
	JB	HOR_KOKATUTA, E6_HORIZONTALA_KOKATUTA
	MOV	GERTAERA, #0x02
	RET
E6_HORIZONTALA_KOKATUTA:
	JB	BERT_KOKATUTA, E6_HOR_BERT
	MOV 	GERTAERA, #0x03
	RET
E6_HORIZONTALA:
	CLR	TICK_ADC0
	MOV	A,ADCH
	CLR	C
	CJNE	A,#0x66,E6_HOR_EZ
	E6_HOR_BAI:
	JB	BERT_KOKATUTA, E6_HOR_BERT
	MOV	GERTAERA,#0x04
	RET
E6_HOR_EZ:
	JC	E6_HOR_BAI
	JB	BERT_KOKATUTA, E6_BERT_ONDO
	MOV	GERTAERA,#0x03
	RET
E6_BERT_ONDO:
	MOV	GERTAERA,#0x00
	RET
E6_HOR_BERT:
	MOV	GERTAERA, #0x06
	RET
E6_BERTIKALA:
	CLR	TICK_ADC1
	MOV	A,ADCH
	CLR	C
	CJNE	A,#0x66,E6_BERT_EZ
	E6_BERT_BAI:
	JB	HOR_KOKATUTA, E6_BERT_HOR
	MOV	GERTAERA,#0x05
	RET
E6_BERT_EZ:
	JC	E6_BERT_BAI
	MOV	GERTAERA,#0x00
	RET
E6_BERT_HOR:
	MOV	GERTAERA, #0x07
	RET
EM6_GER0:
	RET
EM6_GER1:
	CLR	BMOV_BACK
	SETB	LEHEN_ALDIA
	SETB	RV_INTO
	SETB	RH_DOWNTO
	ACALL	TIMER_PIZTU
	RET
EM6_GER2:
	ACALL	ADC0_PIZTU
	RET
EM6_GER3:
	ACALL	ADC1_PIZTU
	RET
EM6_GER4:
	CLR	RH_DOWNTO
	SETB	HOR_KOKATUTA
	ACALL	ADC1_PIZTU
	RET
EM6_GER5:
	CLR	RV_INTO
	SETB	BERT_KOKATUTA
	RET
EM6_GER6:
	SETB	HOR_KOKATUTA
	CLR	LEHEN_ALDIA
	CLR	RH_DOWNTO
	SETB	BMOV_BACK
	MOV 	EGOERA,#0x07
	RET	
EM6_GER7:
	SETB	BERT_KOKATUTA
	CLR	LEHEN_ALDIA
	CLR	RV_INTO
	SETB	BMOV_BACK
	MOV 	EGOERA,#0x07
	RET
;****************************EGOERA_7 (IGURKETA AURRERANTZ)***********************
EGOERA_7:
	ACALL	GERT_SOR_7
	MOV	A, GERTAERA
	RL	A
	MOV	DPTR, #LISTA_GERT_7
	JMP	@A+DPTR
LISTA_GERT_7:
	AJMP	EM7_GER0	;EZER EZ
	AJMP	EM7_GER1	;KOTXEA ez detektatuta
	AJMP	EM7_GER2	;TICK 100ms
	AJMP	EM7_GER3	;horizontala<30
	AJMP	EM7_GER4	;horizontala>50
	AJMP	EM7_GER5	;horizontala ondo
	AJMP	EM7_GER6	;kokatuta horizontala<30 (40 edo altuago)
	AJMP	EM7_GER7	;kokatuta horizontala>50 (40 edo txikiago)
	AJMP	EM7_GER8	;bertikala<30 
	AJMP	EM7_GER9	;bertikala>50
	AJMP	EM7_GER10	;bertikala ondo
	AJMP	EM7_GER11	;kokatu bertikala<30 (40 edo altuago)
	AJMP	EM7_GER12	;kokatu bertikala>50 (40 edo txikiago)
GERT_SOR_7:
	JNB	S_CAR, E7_KOTXEA_KENDU
	JB	TICK_ADC0, E7_HORIZONTALA	;adc salta cuando ha hecho la conversión HORIZONTAL
	JB	TICK_ADC1, E7_BERTIKALA		;adc salta cuando ha hecho la conversión BERTIKAL
	JB	TICK_100ms, E7_TIMER
	MOV	GERTAERA,#0x00
	RET
E7_KOTXEA_KENDU:
	CLR	HOR_KOKATUTA
	CLR	BERT_KOKATUTA
	ACALL	ADC_AMATATU
	MOV	GERTAERA,#0x01
	RET
E7_TIMER:
	CLR	TICK_100ms
	MOV	GERTAERA,#0x02
	RET
E7_HOR_KOKATU30:
	JB	HANDIAGOAH, E7_HOR_KOKATU50
	MOV	A,ADCH
	CLR	C
	CJNE	A,#0x66, E7_EZ40H30	;40 EZ BADA
	E7_HANDIAGO40_HOR:
	MOV	GERTAERA,#0x06
	AJMP	E7_HOR1_AMA
	E7_EZ40H30:
	JNC	E7_HANDIAGO40_HOR
	MOV	GERTAERA,#0x03
	E7_HOR1_AMA:
	RET
E7_HOR_KOKATU50:
	MOV	A,ADCH
	CLR	C
	CJNE	A,#0x66, E7_EZ40H50	;40 EZ BADA
	E7_TXIKIAGO40_HOR:
	MOV	GERTAERA,#0x07
	AJMP	E7_HOR2_AMA
	E7_EZ40H50:
	JC	E7_TXIKIAGO40_HOR
	MOV	GERTAERA,#0x04
	E7_HOR2_AMA:
	RET
E7_HORIZONTALA:
	CLR	TICK_ADC0
	JNB	HOR_KOKATUTA, E7_HOR_KOKATU30
	MOV	A,ADCH
	CLR	C
	CJNE	A,#0x4D, E7_EZ30H	;30 EZ BADA
	E7_TXIKIAGO30_HOR:
	MOV	GERTAERA,#0x03
	AJMP	E7_HOR_AMA
	E7_EZ30H:
	JC	E7_TXIKIAGO30_HOR
	CLR	C
	CJNE	A,#0x80,E7_EZ50H	;50 EZ BADA
	E7_HANDIAGO50_HOR:
	MOV	GERTAERA,#0x04
	AJMP	E7_HOR_AMA
	E7_EZ50H:
	JNC	E7_HANDIAGO50_HOR
	MOV	GERTAERA,#0x05
	E7_HOR_AMA:
	RET
E7_BERT_KOKATU30:
	JB	HANDIAGOAB, E7_BERT_KOKATU50
	MOV	A,ADCH
	CLR	C
	CJNE	A,#0x66, E7_EZ40B30	;40 EZ BADA
	E7_HANDIAGO40_BERT:
	MOV	GERTAERA,#0x0B
	AJMP	E7_BERT1_AMAITU
	E7_EZ40B30:
	JNC	E7_HANDIAGO40_BERT
	MOV	GERTAERA,#0x08
	E7_BERT1_AMAITU:
	RET
E7_BERT_KOKATU50:
	MOV	A,ADCH
	CLR	C
	CJNE	A,#0x66, E7_EZ40B50	;40 EZ BADA
	E7_TXIKIAGO40_BERT:
	MOV	GERTAERA,#0x0C
	AJMP	E7_BERT2_AMAITU
	E7_EZ40B50:
	JC	E7_TXIKIAGO40_BERT
	MOV	GERTAERA,#0x09
	E7_BERT2_AMAITU:
	RET
E7_BERTIKALA:
	CLR	TICK_ADC1
	JNB	BERT_KOKATUTA, E7_BERT_KOKATU30
	MOV	A,ADCH
	CLR	C
	CJNE	A,#0x4D, E7_EZ30B	;30 EZ BADA
	E7_30BAINO_TXIKIAGOAB:
	MOV	GERTAERA,#0x08
	AJMP	E7_BERT_AMA
	E7_EZ30B:
	JC	E7_30BAINO_TXIKIAGOAB
	CLR	C
	CJNE	A,#0x80,E7_EZ50B	;50 EZ BADA
	E7_50BAINO_HANDIAGOAB:
	MOV	GERTAERA,#0x09
	AJMP	E7_BERT_AMA
	E7_EZ50B:
	JNC	E7_50BAINO_HANDIAGOAB
	CLR	TICK_ADC1
	MOV	GERTAERA,#0x0A
	E7_BERT_AMA:
	RET
EM7_GER0:
	RET
EM7_GER1:
	ACALL	TIMER_AMATATU
	CLR	BMOV_BACK
	CLR	HANDIAGOAH
	CLR	HANDIAGOAB
	SETB	RH_UPTO
	MOV	EGOERA,#0x08
	ACALL	TIMER_PIZTU
	RET
EM7_GER2:
	ACALL	ADC0_PIZTU
	RET
EM7_GER3:
	JB	LEHENAH, EM7_GER3_JAR
	SETB	RH_UPTO
	SETB	LEHENAH
	CLR	HOR_KOKATUTA
	EM7_GER3_JAR:
	ACALL	ADC1_PIZTU
	RET
EM7_GER4:
	JB	LEHENAH, EM7_GER4_JAR
	SETB	RH_DOWNTO
	SETB	LEHENAH
	SETB	HANDIAGOAH
	CLR	HOR_KOKATUTA
	EM7_GER4_JAR:
	ACALL	ADC1_PIZTU
	RET
EM7_GER5:
	CLR	RH_UPTO
	CLR	RH_DOWNTO
	ACALL	ADC1_PIZTU
	RET
EM7_GER6:
	SETB	HOR_KOKATUTA
	CLR	LEHENAH
	CLR	RH_UPTO
	ACALL	ADC1_PIZTU
	RET
EM7_GER7:
	CLR	HANDIAGOAH
	SETB	HOR_KOKATUTA
	CLR	LEHENAH
	CLR	RH_DOWNTO
	ACALL	ADC1_PIZTU
	RET
EM7_GER8:
    	JB    LEHENAB, EM7_GER8_JAR
    	SETB  RV_OUTTO
    	SETB  LEHENAB
 	CLR   BERT_KOKATUTA
	EM7_GER8_JAR:
    	RET
EM7_GER9:
    	JB    LEHENAB, EM7_GER9_JAR
    	SETB  RV_INTO
    	SETB	LEHENAB
    	SETB	HANDIAGOAB
    	CLR	BERT_KOKATUTA
	EM7_GER9_JAR:
    	RET
EM7_GER10:
	CLR	RV_INTO
	CLR	RV_OUTTO
	RET
EM7_GER11:
    	SETB	BERT_KOKATUTA
    	CLR	LEHENAB
    	CLR   	RV_OUTTO
    	RET	
EM7_GER12:
	CLR	HANDIAGOAB
    	SETB  	BERT_KOKATUTA
    	CLR   	LEHENAB
	CLR   	RV_INTO
    	RET
;****************************EGOERA_8 (IGURKETA BERTIKALA AURREALDEA)***********************
EGOERA_8:
	ACALL	GERT_SOR_8
	MOV	A, GERTAERA
	RL	A
	MOV	DPTR, #LISTA_GERT_8
	JMP	@A+DPTR
LISTA_GERT_8:
	AJMP	EM8_GER0	;ezer ez
	AJMP	EM8_GER1	;arrabola horizontala kenduta
	AJMP	EM8_GER2	;40cm ibili
	AJMP	EM8_GER3	;arrabola bertikalak erdian
	AJMP	EM8_GER4	;arrabola bertikalak kanpoan
GERT_SOR_8:
	JB	TICK_4s, E8_TIMER
	JB	EGINDA,E8_KANPOAN
	JB	FC_RV_CENTRO,E8_ERDIAN
	JB	FC_RH_TOP, E8_ARRABOLA_GOIAN
E8_EZER_EZ:
	MOV	GERTAERA,#0x00
	RET
E8_ARRABOLA_GOIAN:
	JB	LEHEN_ALDIA, E8_EZER_EZ
	MOV	GERTAERA, #0x01
	RET
E8_ERDIAN:
	MOV	GERTAERA,#0X03
	RET
E8_KANPOAN:
	JNB	FC_RV_BORDE,E8_EZER_EZ
	MOV	GERTAERA,#0x04
	RET
E8_TIMER:
	MOV	GERTAERA,#0x02
	RET
EM8_GER0:
	RET
EM8_GER1:
	SETB	LEHEN_ALDIA
	SETB	BMOV_BACK
	CLR	RH_UPTO
	ACALL	TIMER_PIZTU
	RET
EM8_GER2:
	ACALL	TIMER_AMATATU
	CLR	BMOV_BACK
	SETB	RV_INTO
	RET
EM8_GER3:
	SETB	EGINDA
	CLR	RV_INTO
	SETB	RV_OUTTO
	RET
EM8_GER4:
	CLR	EGINDA
	CLR	LEHEN_ALDIA
	CLR	RV_OUTTO
	MOV	PWM0, #0xFF
	SETB	BMOV_FRONT
	MOV	EGOERA,#0x09
	RET
;****************************EGOERA_9 (XABOIA KENDU)***********************
EGOERA_9:
	ACALL	GERT_SOR_9
	MOV	A, GERTAERA
	RL	A
	MOV	DPTR, #LISTA_GERT_9
	JMP	@A+DPTR
LISTA_GERT_9:
	AJMP	EM9_GER0	;EZER EZ
	AJMP	EM9_GER1	;KOTXEA DAGO
	AJMP	EM9_GER2	;KOTXEA KENDUTA
	AJMP	EM9_GER3	;TICK 4s
GERT_SOR_9:
	JB	TICK_4s, E9_TIMER
	JB	S_CAR, E9_KOTXEA_DETEKTATUTA
	JNB	S_CAR, E9_KOTXEA_KENDUTA
E9_JARRAITU:
	MOV	GERTAERA,#0x00
	RET
E9_TIMER:
	MOV	GERTAERA,#0x03
	RET
E9_KOTXEA_DETEKTATUTA:
	JB	LEHEN_ALDIA,E9_JARRAITU
	MOV	GERTAERA, #0x01
	RET
E9_KOTXEA_KENDUTA:
	JNB	LEHEN_ALDIA,E9_JARRAITU
	JB	EGINDA,E9_JARRAITU
	MOV	GERTAERA,#0x02
	RET
EM9_GER0:
	RET
EM9_GER1:
	SETB	LEHEN_ALDIA
	SETB	EV_AGUA
	RET
EM9_GER2:
	SETB	EGINDA
	CLR	EV_AGUA
	ACALL	TIMER_PIZTU
	RET
EM9_GER3:
	ACALL	TIMER_AMATATU
	CLR	LEHEN_ALDIA
	CLR	BMOV_FRONT
	SETB	BMOV_BACK
	MOV	EGOERA,#0x0A
	RET
;****************************EGOERA_10 (LEHORTU)***********************	
EGOERA_10:
	ACALL	GERT_SOR_10
	MOV	A, GERTAERA
	RL	A
	MOV	DPTR, #LISTA_GERT_10
	JMP	@A+DPTR
LISTA_GERT_10:
	AJMP	EM10_GER0	;EZER EZ
	AJMP	EM10_GER1	;KOTZEA DETEKTATUTA LEHEN ALDIA
	AJMP	EM10_GER2	;KOTZEA KENDUTA
	AJMP	EM10_GER3	;HASIERAKO POSIZIOA
GERT_SOR_10:
	JB	S_CAR,E10_KOTXEA_DETEKTATUTA
	JB	BPOS_START,E10_HASIERAKO_POS
	JNB	S_CAR,E10_KOTXEA_KENDUTA
E10_JARRAITU:
	MOV	GERTAERA,#0x00
	RET
E10_KOTXEA_DETEKTATUTA:
	JB	LEHEN_ALDIA, E10_JARRAITU
	MOV	GERTAERA,#0x01
	RET
E10_KOTXEA_KENDUTA:
	JNB	LEHEN_ALDIA, E10_JARRAITU
	MOV	GERTAERA,#0x02
	RET
E10_HASIERAKO_POS:
	MOV	GERTAERA,#0x03
	RET
EM10_GER0:
	RET
EM10_GER1:
	SETB	LEHEN_ALDIA
	MOV	PWM1,HAIZE_ABIADURA
	RET
EM10_GER2:
	MOV	PWM1,#0xFF
	RET
EM10_GER3:
	CLR	LEHEN_ALDIA
	ACALL	TIMER_PIZTU
	CLR	BMOV_BACK
	CLR	SEM_ROJO
	SETB	SEM_VERDE
	MOV	EGOERA,#0x0B
	RET
;****************************EGOERA_11 (KADENTZIA)***********************	
EGOERA_11:
	ACALL	GERT_SOR_11
	MOV	A, GERTAERA
	RL	A
	MOV	DPTR, #LISTA_GERT_11
	JMP	@A+DPTR
LISTA_GERT_11:
	AJMP	EM11_GER0	;EZER EZ
	AJMP	EM11_GER1	;SEMAFOROA PIZTUTA
	AJMP	EM11_GER2	;SEMAFOROA ITZALITA
	AJMP	EM11_GER3	;KOTZEA KENDUTA
GERT_SOR_11:
	JB	TICK_1s, E11_SEMAFOROA
	JNB	S_PLAT,E11_KOTXEA_KENDUTA
	MOV	GERTAERA,#0x00
	RET
E11_SEMAFOROA:
	JB	SEM_VERDE, E11_PIZTUTA
	MOV	GERTAERA,#0x02
	RET
E11_KOTXEA_KENDUTA:
	MOV	GERTAERA,#0x03
	RET
E11_PIZTUTA:
	MOV	GERTAERA,#0x01
	RET
EM11_GER0:
	RET
EM11_GER1:
	ACALL	TIMER_AMATATU
	ACALL	TIMER_PIZTU
	CLR	SEM_VERDE
	RET
EM11_GER2:
	ACALL	TIMER_AMATATU
	ACALL	TIMER_PIZTU
	SETB	SEM_VERDE
	RET
EM11_GER3:
	ACALL	TIMER_AMATATU
	SETB	SEM_VERDE
	MOV	EGOERA,#0x00
	RET
;**************************************** ADC************************************

ADC0_PIZTU:
	CLR	TICK_ADC0
	CLR	TICK_ADC1
	ANL	ADCON, #0xD8	; ADEX eta ADDRESS 0-ra jarri (1101 1000)
	ORL	ADCON, #0x00	;P5.0 channel 000
	;IEN0
	ORL 	IEN0, #0xC0		; 1100 0000	EA eta EAD piztu
	ORL	ADCON, #0x08 	; ADCS=1
	RET	
ADC1_PIZTU:
	CLR	TICK_ADC0
	CLR	TICK_ADC1
	ANL	ADCON, #0xD8	; ADEX eta ADDRESS 0-ra jarri (1101 1000)
	ORL	ADCON, #0x01	; P5.1 channnel 001
	;IEN0
	ORL 	IEN0, #0xC0		; 1100 0000	EA eta EAD piztu
	ORL	ADCON, #0x08 ; ADCS=1
	RET
ADC_AZPI:
	MOV	A, ADCON		;ADCON-eko edukia A metagailura mugitu da
	ANL	A, #0x01		; ADCI=0	;AND hau egitean 4.bita garbitzen da eta A/D bihurgailua bukatu da
	CJNE	A, #0x01,ET_HOR		;A != 0x01, orduan ET_HOR etiketara salto egingo
	SETB	TICK_ADC1		;
	AJMP	ET_JARRAITU		;
	ET_HOR:
	SETB	TICK_ADC0
	ET_JARRAITU:
	RET
ADC_AMATATU:
	CLR	TICK_ADC0
	CLR	TICK_ADC1
	ANL	IEN0,#0xBF
	RET
;**************************************** TIMER ************************************
TIMER_PIZTU:
	MOV	KONT_50ms, #0x00
	MOV	KONT_1s, #0x00
	CLR	TICK_100ms
	CLR	TICK_1s
	CLR	TICK_4s
	CLR 	TICK_30s
	MOV	TL0, #0xB0		;hasten den bakoitzean beti
	MOV	TH0, #0x30		;hasten den bakoitzean beti
	; IEN0
	SETB	ET0			;baimenak eman etena egiteko
	SETB	EA			;pizteko botoia
	SETB	TCON.4			;timer 0 piztu
	;ORL	TCON,#0x10
	RET
TIMER_AMATATU:
	CLR	TICK_100ms
	CLR	TICK_1s
	CLR	TICK_4s
	CLR	TICK_30s
	CLR	TR0
	CLR	EA
	CLR	ET0
	RET
TIMER_AZPI:
	MOV	TL0, #0xB0		;TL0 eta TH0 tenporizadoreen erregistroa konfiguratzen ditu
	MOV	TH0, #0x3C
	INC	KONT_50ms
	MOV	A,KONT_50ms		;kont 50ms 1 handitu
	CLR	C
	CJNE	A,#0X02,TIMER_1s	; 50ms x 2 = 100ms
	MOV	KONT_50ms,#0x00
	SETB	TICK_100ms
	INC	KONT_100ms
	TIMER_1s:
	JC 	FLAG_BUKATU
	MOV	A,KONT_100ms			;50ms x 20 = 1s
	CLR	C
	CJNE 	A, #0x0A, TIMER_4s
	MOV	KONT_100ms,#0x00
	INC	KONT_1s
	SETB	TICK_1s
	TIMER_4s:
	JC	FLAG_BUKATU
	MOV 	A, KONT_1s
	CLR	C
	CJNE	A, #0x04,FLAG_JARRAITU	;compare and jump if not equals
	SETB	TICK_4s
	FLAG_JARRAITU:
	JC	FLAG_BUKATU		;carry piztuta badago saltam 4S BAINO GUTXIAGO DITUGULAKO
	CJNE	A,#0x1E, FLAG_BUKATU
	SETB	TICK_30s
	MOV	KONT_1s, #0x00		;30s-etara heltzean segunduak 0ra jarri
	FLAG_BUKATU:
	RET
END